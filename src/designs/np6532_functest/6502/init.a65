    .segment "INIT"
    .org    $FC00

    ; change NMI and IRQ vectors
    ; note IRQ vector has been moved from FFFE to FFF8 for this test!
    LDA #0
    STA $FFFA
    STA $FFF8
    LDA #$F0
    STA $FFFB ; NMI = F000
    LDA #$F1
    STA $FFF9 ; IRQ = F100

    ; vector cache coherency
    LDX #$FA
_L1:
    LDA $FF00,X
    STA $FF00,X
    INX
    BNE _L1

    ; zero page and stack cache coherency
_L2:
    LDA $00,X
    STA $00,X
    LDA $100,X
    STA $100,X
    INX
    BNE _L2

; --------------------------------------------------------------------------------

    ; tiny self modifying code test
    SEC
    INC _L3+1   ; change target of BCS from _L4 to _L5
_L3:
    BCS _L4
_L4:
    CLC
_L5:
    BCC _L5     ; spin here on fail

; --------------------------------------------------------------------------------

    ; initial register values
    LDX #$FD
    TXS         ; S = $FD (like T65)
    LDA #$34
    PHA
    LDA #0      ; A = 0
    TAX         ; X = 0
    TAY         ; Y = 0
    PLP         ; P = $34

    ; jump to test code
    JMP $0400
